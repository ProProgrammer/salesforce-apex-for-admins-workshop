---
layout: module
title: Module 4.2&#58; Exercise - Edit and test the Trigger
---

This trigger currently creates a new project record for ANY opportunity that closes. Let's add some additional criteria to make it more selective. 

**The criteria we want to add is: **

1. Opportunity Type = New Customer

2. Expected Revenue > $20,000



The criteria we currently have is Opportunity Stage = Closed Won
![](http://i.imgur.com/IEaA9st.jpg)


### Add Additional Criteria to IF Statement
In order to add our additional criteria, let's modify the IF statement in line 4 of our trigger to include

&& o.Type == 'New Customer'

&& o.ExpectedRevenue > 20000



Our new trigger should look like this: 


```
trigger NewProjTrigger on Opportunity (before update) {
    
    // Create an opportunity list to store all opportunities that fired the trigger and fit the conditional criteria below
    /*** BEST PRACTICE BULKIFICATION: Loop through Trigger.new and store records you want to work with to a list ***/
    List<Opportunity> opps = new List<Opportunity>();
    for (Opportunity thisOpp : Trigger.new)
    {
        //If the opportunity stage is 'Closed Won' and the type is 'New Customer' and the expected revenue is greater than $20k
        if(thisOpp.StageName == 'Closed Won' && thisOpp.Type == 'New Customer' && thisOpp.ExpectedRevenue > 20000)
        {
            opps.add(thisOpp);
        }
    }
    
    if(opps.size() > 0){
        // Run a SOQL query to find all projects related to any opportunities added to the opps list above
        /*** BEST PRACTICE BULKIFICATION: Always do SOQL queries outside of a FOR/FOREACH loop ***/
        List<Milestone1_Project__c> projects = [Select Opportunity__c from Milestone1_Project__c 
                                            where Opportunity__c in: opps];
        
        //Create a separate list to store new projects into
        List<Milestone1_Project__c> projectsToInsert = new List<Milestone1_Project__c>();
        
        // Loop through the list of projects returned from the SOQL query, and create a new project for 
        // each closed opportunity that does not already have an implementation project associated to it
        for(Opportunity thisOpp : opps){
            // Create a flag to signifiy if a project already exists for this opportunity.
            // This will automatically reset to false every time we run through the loop 
            boolean projectAlreadyExistsFlag = false;
            
            // This nested loop of of projects will check each project returned from the SOQL statement against the opportunity
            for(Milestone1_Project__c thisProj : projects){
                // If there is a project found already related to the opportunity, set the projectAlreadyExists flag to true
                // so that we do not create a new project related to this opportunity
                if(thisOpp.Id == thisProj.Opportunity__c){
                    projectAlreadyExistsFlag = true;
                }
            }
            
            // If a project was not found, create a new project related to the closed/won opportunity and name it 
            // after the opportunity
            if(projectAlreadyExistsFlag == false){
                Milestone1_Project__c newProj = new Milestone1_Project__c(Opportunity__c = thisOpp.Id, Name = thisOpp.Name);
                projectsToInsert.add(newProj);
            }
        }
        
        // Insert the list of newly created projects
        /*** BEST PRACTICE BULKIFICATION: Always do a DML operation (insert, update, delete) outside of a FOR/FOREACH ***/
        if(projectsToInsert.size() > 0){
            insert projectsToInsert;
        }
    }
}
```

### Test Updated Trigger
Test this updated trigger by going to an opportunity records and modifying the 2 new criteria (Type and Expected Revenue) and then mark the opportunity Closed Won. 



<div class="row" style="margin-top:40px;">
<div class="col-sm-12">
<a href="4.1-exercise-adding-a-trigger.html" class="btn btn-default"><i class="glyphicon glyphicon-chevron-left"></i> Previous</a>
<a href="5-apex-test-class.html" class="btn btn-default pull-right">Next <i class="glyphicon glyphicon-chevron-right"></i></a>
</div>
</div>
